unit UPedidoVenda;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Data.DB, Vcl.Grids, Vcl.DBGrids,
  Datasnap.DBClient, Vcl.StdCtrls, Vcl.ExtCtrls, FireDAC.Phys.MySQLDef,
  FireDAC.Stan.Intf, FireDAC.Phys, FireDAC.Phys.MySQL, Vcl.Buttons, Vcl.ComCtrls;

type
  TFrmPedidoVenda = class(TForm)
    pnlInferior: TPanel;
    btnFechar: TButton;
    btnConfirmar: TButton;
    pnlCabecalho: TPanel;
    pnlItens: TPanel;
    CDSItens: TClientDataSet;
    DItens: TDataSource;
    DBGridItens: TDBGrid;
    pnlCadastroItem: TPanel;
    EditCliente: TLabeledEdit;
    btnPesqPedidos: TSpeedButton;
    DateTimePicker1: TDateTimePicker;
    Label1: TLabel;
    EditNomeCliente: TEdit;
    EditVlrTotal: TLabeledEdit;
    Panel1: TPanel;
    btnInserirItem: TButton;
    btnAlterarItem: TButton;
    btnExcluirItem: TButton;
    CDSItensProduto: TIntegerField;
    CDSItensDescricao: TStringField;
    CDSItensQuantidade: TFloatField;
    CDSItensVlr_Unit: TFloatField;
    CDSItensVlr_Total: TFloatField;
    Panel2: TPanel;
    BtnCancelarItem: TButton;
    btnGravarItem: TButton;
    EditProduto: TLabeledEdit;
    EditDescricaoProduto: TEdit;
    EditVlrUnit: TLabeledEdit;
    EditQtde: TLabeledEdit;
    lblTotalPedido: TLabel;
    procedure btnFecharClick(Sender: TObject);
    procedure EditClienteChange(Sender: TObject);
    procedure EditClienteExit(Sender: TObject);
    procedure EditClienteEnter(Sender: TObject);
    procedure btnExcluirItemClick(Sender: TObject);
    procedure DBGridItensKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
    procedure DBGridItensKeyPress(Sender: TObject; var Key: Char);
    procedure DBGridItensDrawColumnCell(Sender: TObject; const Rect: TRect;
      DataCol: Integer; Column: TColumn; State: TGridDrawState);
    procedure DBGridItensDblClick(Sender: TObject);
    procedure btnAlterarItemClick(Sender: TObject);
    procedure BtnCancelarItemClick(Sender: TObject);
    procedure FormShow(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure EditProdutoExit(Sender: TObject);
    procedure btnGravarItemClick(Sender: TObject);
    procedure btnInserirItemClick(Sender: TObject);
  private
    procedure AbreConexao;
    function StringToValor(pValor: String): Double;
    function ValorToString(pValor: Double; const pMoeda: Boolean = True): String;
    function FormataVlrString(pValor: String): String;
    function ItemValido: Boolean;
    procedure LimparTelaItens;
    procedure CarregaItem;
    procedure CalculaTotal;
    { Private declarations }
  public
    { Public declarations }
  end;

var
  FrmPedidoVenda: TFrmPedidoVenda;

implementation

{$R *.dfm}

uses ClassCliente, ClassConexao, ClassProduto;

var
  Conexao: TConexao;

procedure TFrmPedidoVenda.btnAlterarItemClick(Sender: TObject);
begin
  pnlCadastroItem.Visible := True;
  CarregaItem;
  EditProduto.SetFocus;
end;

procedure TFrmPedidoVenda.CarregaItem;
begin
  EditProduto.Text := CDSItensProduto.AsString;
  EditDescricaoProduto.Text := CDSItensDescricao.AsString;
  EditQtde.Text             := ValorToString(CDSItensQuantidade.AsFloat);
  EditVlrUnit.Text          := ValorToString(CDSItensVlr_Unit.AsFloat);
end;

procedure TFrmPedidoVenda.BtnCancelarItemClick(Sender: TObject);
begin
  LimparTelaItens;
  pnlCadastroItem.Visible := False;
end;

procedure TFrmPedidoVenda.btnExcluirItemClick(Sender: TObject);
begin
  if not CDSItens.IsEmpty then begin
    if Application.MessageBox(PChar('Deseja realmente excluir o produto: ' + CDSItensProduto.AsString + '?'),
                              'Atenção', MB_ICONINFORMATION + MB_YESNO) = mrYes then begin
      CDSItens.Delete;
    end;
  end;
end;

procedure TFrmPedidoVenda.btnFecharClick(Sender: TObject);
begin
  Close;
end;

procedure TFrmPedidoVenda.btnGravarItemClick(Sender: TObject);
begin
  if ItemValido then begin
    CDSItens.Insert;
    CDSItensProduto.AsInteger := StrToInt(EditProduto.Text);
    CDSItensDescricao.AsString:= EditDescricaoProduto.Text;
    CDSItensQuantidade.AsFloat:= StringToValor(EditQtde.Text);
    CDSItensVlr_Unit.AsFloat  := StringToValor(EditVlrUnit.Text);
    CDSItensVlr_Total.AsFloat := CDSItensQuantidade.AsFloat * CDSItensVlr_Unit.AsFloat;
    CDSItens.Post;

    LimparTelaItens;
    CalculaTotal;
  end;
end;

procedure TFrmPedidoVenda.CalculaTotal;
var
  nTotal: Double;
begin
  CDSItens.First;
  nTotal := 0;
  while not CDSItens.Eof do begin
    nTotal := nTotal + CDSItensVlr_Total.AsFloat;
    CDSItens.Next;
  end;

  EditVlrTotal.Text := ValorToString(nTotal);
  lblTotalPedido.Caption := 'Valor Total: ' + ValorToString(nTotal);
end;

procedure TFrmPedidoVenda.btnInserirItemClick(Sender: TObject);
begin
  pnlCadastroItem.Visible := True;
  EditProduto.SetFocus;
end;

procedure TFrmPedidoVenda.LimparTelaItens;
begin
  EditProduto.Text          := '';
  EditDescricaoProduto.Text := '';
  EditQtde.Text             := ValorToString(0, False);
  EditVlrUnit.Text          := ValorToString(0);
end;

function TFrmPedidoVenda.ItemValido: Boolean;
begin
  Result := True;

  if (EditProduto.Text = '') or (EditProduto.Color = clRed) then begin
    Application.MessageBox('Produto inválido. Favor verificar!', 'Atenção', MB_ICONINFORMATION + MB_OK);
    EditProduto.SetFocus;
    Result := False;
  end;

  if StringToValor(EditQtde.Text) = 0 then begin
    Application.MessageBox('Quantidade deve ser informada. Favor verificar!', 'Atenção', MB_ICONINFORMATION + MB_OK);
    EditQtde.SetFocus;
    Result := False;
  end;

  if StringToValor(EditVlrUnit.Text) = 0 then begin
    Application.MessageBox('Valor unitário deve ser informado. Favor verificar!', 'Atenção', MB_ICONINFORMATION + MB_OK);
    EditVlrUnit.SetFocus;
    Result := False;
  end;
end;

procedure TFrmPedidoVenda.DBGridItensDblClick(Sender: TObject);
begin
  btnAlterarItem.Click;
end;

procedure TFrmPedidoVenda.DBGridItensDrawColumnCell(Sender: TObject;
  const Rect: TRect; DataCol: Integer; Column: TColumn; State: TGridDrawState);
begin
  if not CDSItens.IsEmpty then begin
    if not odd(DItens.DataSet.RecNo) then begin
      if not (gdSelected in State) then begin
        DBGridItens.Canvas.Brush.Color := clGradientActiveCaption;
        DBGridItens.Canvas.FillRect(Rect);
        DBGridItens.DefaultDrawDataCell(rect,Column.Field,State);
        DBGridItens.DefaultDrawColumnCell(rect,DataCol,Column,state);
      end;
    end;
  end;
end;

procedure TFrmPedidoVenda.DBGridItensKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
  if Key = VK_DELETE then begin
    btnExcluirItem.Click;
  end;
end;

procedure TFrmPedidoVenda.DBGridItensKeyPress(Sender: TObject; var Key: Char);
begin
  if Key = #13 then begin
    btnAlterarItem.Click;
  end;
end;

procedure TFrmPedidoVenda.EditClienteChange(Sender: TObject);
begin
  btnPesqPedidos.Visible := EditCliente.Text = '';
end;

procedure TFrmPedidoVenda.EditClienteEnter(Sender: TObject);
begin
  TLabeledEdit(Sender).Color := clWindow;
end;

procedure TFrmPedidoVenda.EditClienteExit(Sender: TObject);
var
  Cliente: TCliente;
begin
  if EditCliente.Text <> '' then begin
    Cliente := TCliente.Create;
    try
      if Cliente.ValidaExiste(StrToInt(EditCliente.Text)) then begin
        EditNomeCliente.Text := Cliente.Nome;
        EditCliente.Color    := clGreen;
      end else begin
        Application.MessageBox('Cliente não cadastrado. Favor verificar!', 'Atenção', MB_ICONEXCLAMATION+MB_OK);
        EditCliente.Color := clRed;
      end;
    finally
      Cliente.Free;
    end;
  end else begin
    EditCliente.Color := clRed;
  end;
end;

procedure TFrmPedidoVenda.EditProdutoExit(Sender: TObject);
var
  Produto: TProduto;
begin
  if EditCliente.Text <> '' then begin
    Produto := TProduto.Create;
    try
      if Produto.ValidaExiste(StrToInt(EditProduto.Text)) then begin
        EditDescricaoProduto.Text := Produto.Descricao;
        EditProduto.Color         := clGreen;
      end else begin
        Application.MessageBox('Cliente não cadastrado. Favor verificar!', 'Atenção', MB_ICONEXCLAMATION+MB_OK);
        EditProduto.Color         := clRed;
      end;
    finally
      Produto.Free;
    end;
  end else begin
    EditProduto.Color := clRed;
  end;

end;

procedure TFrmPedidoVenda.FormClose(Sender: TObject; var Action: TCloseAction);
begin
  Conexao.Free;
end;

procedure TFrmPedidoVenda.FormCreate(Sender: TObject);
begin
  Conexao := TConexao.Create;
end;

procedure TFrmPedidoVenda.FormShow(Sender: TObject);
begin
  AbreConexao;
end;

procedure TFrmPedidoVenda.AbreConexao;
begin
  if not Conexao.Conectar then begin
    Application.MessageBox(PChar('Erro ao conectar ao banco de dados. Erro: ' + Conexao.MsgErro), 'Erro', MB_ICONERROR + MB_OK);
  end;
end;

function TFrmPedidoVenda.ValorToString(pValor: Double; const pMoeda: Boolean = True): String;
begin
  if pMoeda then begin
    Result := FormatCurr('#,##', pValor);
  end else begin
    Result := FormatFloat('#,##', pValor);
  end;
end;

function TFrmPedidoVenda.StringToValor(pValor: String): Double;
begin
  Result := StrToFloat(FormataVlrString(pValor));
end;

function TFrmPedidoVenda.FormataVlrString(pValor: String): String;
begin
  Result := StringReplace(pValor, '.', '', [rfReplaceAll]);
end;

end.
